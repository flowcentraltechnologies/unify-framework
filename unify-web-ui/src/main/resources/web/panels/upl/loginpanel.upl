// UPL template for login panel
// Copyright 2018-2025 The Code Department.
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
//Login Panel UPL
!ui-loginpanel layout:$d{!ui-vertical} components:$c{headerPanel mainPanel loginActionPanel}
    formCaptionSuffix:$s{:} loginNameCase:upper loginMessageStyleClass:$e{lpmessage}
    languageList:$s{javalocalelist}
    languagePath:$n{/noResult}
   
!ui-panel:headerPanel layout:$f{headerLayout} components:$f{headerComponentList}
!ui-form:mainPanel captionSuffix:$f{formCaptionSuffix}
    section:$d{!ui-section components:$c{languageField nameField passwordField tokenField loginMsgPanel}}

!ui-select:languageField caption:$m{loginpanel.language} binding:$f{languageBinding}
    list:$f{languageList} blankOption:$s{}
    eventHandler:$d{!ui-event event:onchange action:$c{languageAct}}
!ui-name:nameField caption:$m{loginpanel.username} binding:$g{loginNameBinding}
    case:$f{loginNameCase} focus:true
    eventHandler:$d{!ui-event event:onenter action:$c{loginAct}}
!ui-password:passwordField caption:$m{loginpanel.password} binding:$g{loginPasswordBinding} 
    eventHandler:$d{!ui-event event:onenter action:$c{loginAct}}
!ui-password:tokenField caption:$m{loginpanel.token} binding:$g{loginTokenBinding} 
    eventHandler:$d{!ui-event event:onenter action:$c{loginAct}}
!ui-panel:loginMsgPanel styleClass:$e{lpmessagepanel} components:$c{loginMsg}
!ui-label:loginMsg styleClass:$f{loginMessageStyleClass} binding:$g{loginMessageBinding}

!ui-panel:loginActionPanel layout:$d{!ui-horizontal} components:$c{loginBtn} components:$f{footerComponentList}
!ui-button:loginBtn caption:$m{button.login} eventHandler:$d{!ui-event event:onclick action:$c{loginAct}}

!ui-post:loginAct path:$g{loginPath} components:$c{mainPanel}
!ui-post:languageAct path:$f{languagePath} components:$c{mainPanel}
