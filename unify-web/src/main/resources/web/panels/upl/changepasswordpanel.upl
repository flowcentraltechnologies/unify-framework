//Change Password Panel UPL
!ui-changepasswordpanel layout:$d{!ui-vertical}
    components:$c{changePasswordBodyPanel pwdChangeMsgPanel pwdActionPanel}
    formCaptionSuffix:$s{:} messageStyleClass:$e{cpmessage}

!ui-formpanel:changePasswordBodyPanel layout:$d{!ui-vertical}
    formSection:$d{!ui-section components:$c{oldPassword newPassword confirmPassword}}
    formCaptionSuffix:$f{formCaptionSuffix}
!ui-password:oldPassword binding:$g{oldPasswordBinding} caption:$m{changepasswordpanel.oldpassword} focus:true required:true
!ui-password:newPassword binding:$g{newPasswordBinding} caption:$m{changepasswordpanel.newpassword} required:true
!ui-password:confirmPassword binding:$g{confirmPasswordBinding} caption:$m{changepasswordpanel.confirmpassword} required:true
    eventHandler:$d{!ui-event event:onenter action:$c{changePwdAct}}

!ui-panel:pwdChangeMsgPanel styleClass:$e{cpmessagepanel} components:$c{pwdChangeMsg}
!ui-label:pwdChangeMsg  styleClass:$f{messageStyleClass} binding:$g{messageBinding}
    
!ui-panel:pwdActionPanel layout:$d{!ui-horizontal} components:$c{changePwdBtn cancelPwdBtn}
!ui-button:changePwdBtn caption:$m{button.change} eventHandler:$d{!ui-event event:onclick action:$c{changePwdAct}}
!ui-button:cancelPwdBtn caption:$m{button.cancel} eventHandler:$d{!ui-event event:onclick action:$c{cancelPwdAct}}
  
//Validations
!ui-validation:chgPwdVal components:$c{oldPassword newPassword confirmPassword}
!ui-notsamevalidation:chgNewPwdVal components:$c{newPassword oldPassword}
!ui-samevalidation:chgConfPwdVal components:$c{confirmPassword newPassword}

// Actions  
!ui-post:changePwdAct path:$g{changePasswordPath} validations:$c{chgPwdVal chgNewPwdVal chgConfPwdVal}
    components:$c{oldPassword newPassword confirmPassword}
!ui-post:cancelPwdAct path:$g{cancelChangePasswordPath}
    